# Print a test page using print-job
{
	# The name of the test...
	NAME "Print file using Print-Job"

	# The operation to use
	OPERATION Print-Job

	# Attributes, starting in the operation group...
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR mimeMediaType document-format "application/pdf"

	GROUP job-attributes-tag
	ATTR integer copies 1
	ATTR keyword media "na_letter_8.5x11in"
	ATTR keyword print-scaling "fit"
	ATTR enum orientation-requested 4
	ATTR enum print-quality 3

	FILE $filename

	# What statuses are OK?
	STATUS successful-ok
	STATUS successful-ok-ignored-or-substituted-attributes

	# What attributes do we expect?
	EXPECT job-id
	EXPECT job-uri
	DISPLAY job-id
}
{
	NAME "Wait for job to complete..."
	OPERATION Get-Job-Attributes
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR integer job-id $job-id

	STATUS successful-ok
	EXPECT job-id
	EXPECT job-state WITH-VALUE >5 REPEAT-NO-MATCH
	DISPLAY job-state
	DISPLAY job-state-reasons
}
